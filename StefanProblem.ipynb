{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "92dc91f2-7a02-4bf8-8f20-b86fd3d989ff",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Importing (using/include) packages and files needed for the code to run\n",
    "using SparseArrays\n",
    "using LinearAlgebra\n",
    "using IterativeSolvers\n",
    "using WriteVTK\n",
    "using Printf\n",
    "using Statistics\n",
    "using Dates\n",
    "using SpecialFunctions\n",
    "using Roots\n",
    "using NLsolve\n",
    "using HDF5\n",
    "include(\"Grid.jl\")\n",
    "include(\"Markers.jl\")\n",
    "include(\"Stokes.jl\")\n",
    "include(\"GridOperations.jl\")\n",
    "# Note: that we import pyplot last to avoid a name conflict with grid\n",
    "using PyPlot\n",
    "include(\"Visualization.jl\")\n",
    "include(\"Topo.jl\")\n",
    "include(\"Outputs.jl\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cfe374fc-6e05-4d82-96ac-5cc85cf846aa",
   "metadata": {
    "editable": true,
    "slideshow": {
     "slide_type": ""
    },
    "tags": []
   },
   "source": [
    "# Stefan Problem - test melting and solidification\n",
    "\n",
    "Step 1 - import packages"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ebc2da32-d2ab-4a87-aefa-267589b18944",
   "metadata": {},
   "source": [
    "Step 2 - Define a function that relates entropy and temperature/pressure. \n",
    "$$\n",
    "T = f_1(P,S)\n",
    "$$\n",
    "\n",
    "$$\n",
    "T = \\exp \\left( \\frac{S}{C_V} + \\ln (T_{ref}) - \\frac{\\Delta H_{fus} }{C_V T_M}X \\right)\n",
    "$$\n",
    "\n",
    "and \n",
    "$$\n",
    "S = f_2(P,T)\n",
    "$$\n",
    "$$\n",
    "S = C_V (\\ln (T) - \\ln(T_{ref})) + \\frac{\\Delta H_{fus}}{T_m}X\n",
    "$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "be475d69-a27c-491a-8f58-561dd4dbf014",
   "metadata": {},
   "outputs": [],
   "source": [
    "# functions that relates entropy and temperature/pressure are define in TemperatureEntropy.jl file\n",
    "include(\"TemperatureEntropy.jl\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5ffa255e-9cb4-4563-8d22-88745b0ee668",
   "metadata": {},
   "source": [
    "\n",
    "Step 3 - Write functions to set up the initial condition for the Stefan problem\n",
    "\n",
    "- The box is filled entirely with one material\n",
    "- The sides are insulating\n",
    "- Density is constant and equal to 1000 kg/m$^3$\n",
    "- The surface is at T=100K, the bottom is at T=273 K\n",
    "- The liquid is isothermal with $T=T_m$.\n",
    "- The solid has initial $T(z)$ given by the Stefan solution (Turcotte and Schubert section 4.18)\n",
    "$$\n",
    "\\theta = \\frac{erf(\\eta)}{erf(\\lambda_1)}\n",
    "$$\n",
    "- Given the initial temperature, compute initial entropy S on the markers."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "256f17c1-30a6-4261-949f-261ce3e9103a",
   "metadata": {},
   "outputs": [],
   "source": [
    "function initial_ice_depth(x::Float64)\n",
    "    return options[\"ym\"]\n",
    "end \n",
    "\n",
    "struct Materials\n",
    "    alpha::Vector{Float64} # Thermal expansion (1/K)\n",
    "    rho0::Vector{Float64} # Density (kg/m^3)\n",
    "    Hr::Vector{Float64} # Radiogenic heat production (W/m^3)\n",
    "    Cp::Vector{Float64} # Heat capacity (J/kg*K)\n",
    "    kThermal::Vector{Float64} # Thermal conductivity (W/m*K)\n",
    "    function Materials()\n",
    "        new([0.0],[1e3],[0.0],[2.1e3],[2.2])\n",
    "    end    \n",
    "end\n",
    "\n",
    "function update_marker_prop!(markers::Markers,materials::Materials)\n",
    "    rho = markers.scalarFields[\"rho\"]\n",
    "    T = markers.scalarFields[\"T\"]\n",
    "    X = markers.scalarFields[\"X\"]\n",
    "    S = markers.scalarFields[\"S\"]\n",
    "    mmat = markers.integers[markers.integerFields[\"material\"],:]\n",
    "    for i in 1:markers.nmark\n",
    "        markers.scalars[rho,i] = materials.rho0[mmat[i]]\n",
    "    end\n",
    "end\n",
    "\n",
    "function update_marker_T_X!(markers::Markers,options::Dict)\n",
    "    T = markers.scalarFields[\"T\"]\n",
    "    X = markers.scalarFields[\"X\"]\n",
    "    S = markers.scalarFields[\"S\"]    \n",
    "    for i in 1:markers.nmark\n",
    "        markers.scalars[T,i],markers.scalars[X,i] = compute_T_X_from_S((markers.scalars[S,i]),options)\n",
    "    end\n",
    "end\n",
    "\n",
    "function initial_conditions!(markers::Markers,materials::Materials,options::Dict)\n",
    "    material = markers.integerFields[\"material\"]\n",
    "    S = markers.scalarFields[\"S\"]\n",
    "    X = markers.scalarFields[\"X\"]\n",
    "    T = markers.scalarFields[\"T\"]\n",
    "    alpha = markers.scalarFields[\"alpha\"]\n",
    "    Cp = markers.scalarFields[\"Cp\"]\n",
    "    Hr = markers.scalarFields[\"Hr\"]\n",
    "    kThermal = markers.scalarFields[\"kThermal\"]\n",
    "    \n",
    "    # Setting up Stefan conidtion\n",
    "    lambda1 = get_lambda1(options)\n",
    "    t = get_t(lambda1,options)\n",
    "    \n",
    "    # Note: the melt fraction is defining our layers or ice and water.\n",
    "    # water - X = 1.0 \n",
    "    # ice - X = 0.0\n",
    "    for i in 1:markers.nmark\n",
    "        mx = markers.x[1,i]\n",
    "        my = markers.x[2,i]\n",
    "        hice = initial_ice_depth(mx)\n",
    "        \n",
    "        if my > hice # subsurface global ocean\n",
    "            markers.integers[material,i] = 1\n",
    "            markers.scalars[alpha,i] = materials.alpha[1]\n",
    "            markers.scalars[Cp,i] = materials.Cp[1]\n",
    "            markers.scalars[Hr,i] = materials.Hr[1]\n",
    "            markers.scalars[kThermal,i] = materials.kThermal[1]\n",
    "            markers.scalars[T,i] = 273.0\n",
    "            markers.scalars[X,i] = 1.0\n",
    "            markers.scalars[S,i] = compute_S_from_T_X(markers.scalars[X,i],markers.scalars[T,i],options)\n",
    "        elseif my < hice # icy shell\n",
    "            markers.integers[material,i] = 1\n",
    "            markers.scalars[alpha,i] = materials.alpha[1]\n",
    "            markers.scalars[Cp,i] = materials.Cp[1]\n",
    "            markers.scalars[Hr,i] = materials.Hr[1]\n",
    "            markers.scalars[kThermal,i] = materials.kThermal[1]\n",
    "            markers.scalars[T,i] = stefan_initial_condition(get_theta(my,t,lambda1),options)\n",
    "            markers.scalars[X,i] = 0.0\n",
    "            markers.scalars[S,i] = compute_S_from_T_X(markers.scalars[X,i],markers.scalars[T,i],options)\n",
    "        end\n",
    "    end \n",
    "    # end loop over markers\n",
    "    update_marker_prop!(markers,materials)\n",
    "end\n",
    "\n",
    "function get_interface(grid::CartesianGrid,mat::Matrix{Float64},contour_value::Float64)\n",
    "    # Finding interfaces\n",
    "    interface_position = zeros(Float64,grid.nx+1);\n",
    "    for j in 1:grid.nx+1\n",
    "        i = 1\n",
    "        while i <= grid.ny\n",
    "            if mat[i,j] == contour_value\n",
    "                interface_position[j] = grid.yc[i]\n",
    "                break\n",
    "            elseif mat[i+1,j] < contour_value\n",
    "                # interface is located within this cell.\n",
    "                interface_position[j] = grid.yc[i] + ((grid.yc[i+1]-grid.yc[i])/(mat[i+1,j]-mat[i,j]))*(contour_value-mat[i,j])\n",
    "                break\n",
    "            end\n",
    "            i = i+1\n",
    "        end\n",
    "    end\n",
    "    return interface_position\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5af65739-2045-4d1a-aca8-d1503e48d576",
   "metadata": {},
   "source": [
    "# Main part of code\n",
    "- At each timestep, begin by computing T(S,X) on the markers\n",
    "- Iteratively solve the temperature equation. Start with a guess S_new = S_old\n",
    "    - For T_new, compute $q_{cond} = \\nabla \\cdot (k \\nabla T_{new})$\n",
    "    - Solve diffusion equation for S_new\n",
    "    - Given S_new, update T_new\n",
    "    - Iterate until converged...\n",
    "- Given the new entropy solution on the cell centers, update the entropy and melt fraction on the markers."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "967be0e9-eb23-416e-958c-e99dde115312",
   "metadata": {},
   "outputs": [],
   "source": [
    "function model_run(options::Dict)\n",
    "    W = 1e4\n",
    "    H = 2e4\n",
    "    ny = 50\n",
    "    nx = 51 \n",
    "    gx = 0.0\n",
    "    gy = 0.113\n",
    "    \n",
    "    # -1 = insulating, 1 = constant temp \n",
    "    Tbctype = [-1,-1,1,1] #left, right, top, bottom\n",
    "    Tbcval = [0.0,0.0,100.0,273.0] #left, right, top, bottom\n",
    "    markx = 6\n",
    "    marky = 6\n",
    "    seconds_in_year = 3.15e7\n",
    "    # plot interval should be in seconds\n",
    "    plot_interval = 1e6*seconds_in_year # 1 Myr \n",
    "    \n",
    "    end_time = 3e7*seconds_in_year\n",
    "    dtmax = plot_interval\n",
    "    grid = CartesianGrid(W,H,nx,ny)\n",
    "    println(\"Grid resolution(ny x nx) : $ny x $nx\")\n",
    "    println(\"Cell size in the x-direction is $(grid.W/(grid.nx-1))\")\n",
    "    println(\"Cell size in the y-direction is $(grid.H/(grid.ny-1))\")\n",
    "    \n",
    "    materials = Materials()\n",
    "    println(\"Creating Markers...\")\n",
    "    @time markers = Markers(grid,[\"alpha\",\"T\",\"rho\",\"Cp\",\"Hr\",\"kThermal\",\"S\",\"X\"],[\"material\"] ; nmx=markx,nmy=marky,random=false)\n",
    "    println(\"Initial condition...\")\n",
    "    @time initial_conditions!(markers,materials,options)\n",
    "\n",
    "    local time_plot = []\n",
    "    local X_contour = []\n",
    "    local X_contour_array = []\n",
    "    \n",
    "    ### Setting up agruments for termination criteria ###\n",
    "    max_step::Int64=-1\n",
    "    max_time::Float64=-1.0\n",
    "    max_time = max_time == -1.0 ? typemax(Float64) : max_time\n",
    "    max_step = max_step == -1 ? typemax(Int64) : max_step\n",
    "\n",
    "    time = 0.0\n",
    "    iout= 0\n",
    "    last_plot = 0.0\n",
    "    dt = dtmax\n",
    "    \n",
    "    rho_c = nothing\n",
    "    kThermal_vx = nothing\n",
    "    kThermal_vy = nothing\n",
    "    Hr = nothing\n",
    "    kThermal = nothing\n",
    "    dTmax = nothing\n",
    "    dTemp = nothing\n",
    "    Tlast = nothing\n",
    "    Slast = nothing\n",
    "    Xlast = nothing\n",
    "    q_vx = nothing\n",
    "    q_vy = nothing\n",
    "    melt_fraction_contour = nothing\n",
    "    Tnew = nothing\n",
    "    Snew = nothing \n",
    "    Xnew = nothing\n",
    "\n",
    "    # Initial\n",
    "    ### Transfer properties markers -> nodes ###\n",
    "    # Cell Centers\n",
    "    Xlast,Tlast = marker_to_stag(markers,grid,[\"X\",\"T\"],\"center\")\n",
    "    # Interpolating entropy using rhoT as weight\n",
    "    rhoT = markers.scalars[markers.scalarFields[\"rho\"],:] .* markers.scalars[markers.scalarFields[\"T\"],:]\n",
    "    Slast, = marker_to_stag(markers,grid,[\"S\"],\"center\",extra_weight=rhoT)  \n",
    "    # Projecting Slast from the cell centers to the markers\n",
    "    cell_center_to_markers!(markers,grid,Slast,\"S\")\n",
    "    # Updating Temperature and Melt fraction on the markers\n",
    "    update_marker_T_X!(markers,options)\n",
    "\n",
    "    ### Initial Plots ###\n",
    "    get_plots(grid,Slast,Tlast,Xlast,\"initial\")\n",
    "    \n",
    "    itime = 1 \n",
    "    output_dir = \"output_subgrid_diff\"\n",
    "    terminate = false\n",
    "    while !terminate\n",
    "\n",
    "        ### update the markers properties ###\n",
    "        update_marker_prop!(markers,materials)\n",
    "        ### Transfer properties markers -> nodes ###\n",
    "        # Cell Centers\n",
    "        Xlast_new,Tlast_new = marker_to_stag(markers,grid,[\"X\",\"T\"],\"center\")\n",
    "        rho_c_new,Hr_new,kThermal_new = marker_to_stag(markers,grid,[\"rho\",\"Hr\",\"kThermal\"],\"center\")\n",
    "        # Interpolating entropy using rhoT as weight\n",
    "        rhoT = markers.scalars[markers.scalarFields[\"rho\"],:] .* markers.scalars[markers.scalarFields[\"T\"],:]\n",
    "        Slast_new, = marker_to_stag(markers,grid,[\"S\"],\"center\",extra_weight=rhoT)       \n",
    "        # Vx and Vy nodes:\n",
    "        kThermal_vx_new, = marker_to_stag(markers,grid,[\"kThermal\",],\"vx\")\n",
    "        kThermal_vy_new, = marker_to_stag(markers,grid,[\"kThermal\",],\"vy\")\n",
    "        \n",
    "        # Copy field data\n",
    "        rho_c = copy(rho_c_new)\n",
    "        kThermal_vx = copy(kThermal_vx_new)\n",
    "        kThermal_vy = copy(kThermal_vy_new)\n",
    "        Hr = copy(Hr_new)\n",
    "        kThermal = copy(kThermal_new)\n",
    "        Tlast = copy(Tlast_new)    \n",
    "        Slast = copy(Slast_new)\n",
    "        Xlast = copy(Xlast_new)\n",
    "\n",
    "        diffusion_timestep = calculate_diffusion_timestep(grid,options)\n",
    "        if itime > 1\n",
    "            this_dtmax = min(1.2*dt,dtmax)\n",
    "        else\n",
    "            this_dtmax = dtmax\n",
    "        end\n",
    "        if this_dtmax > diffusion_timestep\n",
    "            dt = diffusion_timestep\n",
    "        else \n",
    "            dt = this_dtmax\n",
    "        end\n",
    "\n",
    "        last_T_norm = NaN\n",
    "        T_norm = NaN\n",
    "        dT = nothing\n",
    "        dTmax = Inf\n",
    "        dS = nothing\n",
    "        dSmax = Inf\n",
    "        tolerance = 1e-8\n",
    "        dTnorm = []\n",
    "        ititer = []\n",
    "        titer = 1 \n",
    "        max_titer = 300\n",
    "        for titer=1:max_titer\n",
    "            \n",
    "            # Computing conductive heat flux (using Tlast yields an explicit scheme)\n",
    "            q_vx,q_vy = compute_q_cond(grid,Tlast,kThermal_vx,kThermal_vy) # W/m^2\n",
    "            \n",
    "            # Computing the new entropy (using Tlast yields an explicit scheme)\n",
    "            Snew = compute_S_new(grid,Tlast,rho_c,Hr,q_vx,q_vy,Slast,dt);\n",
    "    \n",
    "            # Updating the new temperature and new melt fraction from the new entropy\n",
    "            Tnew,Xnew = update_T_X_from_S(Snew,options)             \n",
    "            Tnew,Xnew,Snew = ghost_nodes_center_TXS(grid,Tnew,Xnew,Snew,Tbctype,Tbcval,options)\n",
    "\n",
    "            # Computing residual for entropy\n",
    "            residual_S = compute_entropy_residual(grid,Tlast,rho_c,Hr,q_vx,q_vy,Slast,Snew,dt)\n",
    "            Snorm = norm(residual_S[2:ny,2:nx])\n",
    "       \n",
    "            if titer > 1\n",
    "                last_T_norm = T_norm;\n",
    "                T_norm = norm(Tnew[2:grid.ny,2:grid.nx]);\n",
    "                push!(dTnorm,abs(T_norm-last_T_norm))\n",
    "                push!(ititer,titer)\n",
    "            else\n",
    "                T_norm = norm(Tnew[2:end-1,2:end-1]);\n",
    "                push!(dTnorm,abs(T_norm-last_T_norm))\n",
    "                push!(ititer,titer)\n",
    "            end\n",
    "            \n",
    "            # Computing the maximum temperature change\n",
    "            dT = Tnew - Tlast\n",
    "            dTmax = maximum(abs.(dT[2:grid.ny,2:grid.nx]))\n",
    "\n",
    "            # Computing the maximum entropy change\n",
    "            dS = Snew - Slast\n",
    "            dSmax = maximum(abs.(dS[2:grid.ny,2:grid.nx]))\n",
    "\n",
    "            # Checking for convergence:\n",
    "            if Snorm < tolerance\n",
    "                break\n",
    "            elseif titer == max_titer\n",
    "                terminate = true\n",
    "                @error(\"Did not converged\")\n",
    "            iout += 1\n",
    "            elseif any(isnan.(dT))\n",
    "                terminate = true\n",
    "                @error(\"NaN or Inf apperred\")\n",
    "            end\n",
    "        end\n",
    "\n",
    "        # Updating entropy on the markers by projecting dS from the cell centers to the markers\n",
    "        # cell_center_change_to_markers!(markers,grid,dS,\"S\")\n",
    "        # update_marker_T_X!(markers,options)\n",
    "        dS_subgrid_node = subgirdSdiff!(grid,markers,Slast,dt,options,diff_coeff=1.0)\n",
    "        dS_remaining = dS-dS_subgrid_node\n",
    "        cell_center_change_to_markers!(markers,grid,dS_remaining,\"S\")\n",
    "        update_marker_T_X!(markers,options)\n",
    "\n",
    "        Slast_new = copy(Snew)\n",
    "        Xlast_new = copy(Xnew)\n",
    "        Tlast_new = copy(Tnew)\n",
    "        \n",
    "        melt_fraction_contour = get_interface(grid,-Xnew,-0.5)\n",
    "        append!(X_contour_array,[melt_fraction_contour])\n",
    "        avg_melt_fraction_contour = mean(melt_fraction_contour)\n",
    "        append!(X_contour,avg_melt_fraction_contour)\n",
    "        append!(time_plot,time)  \n",
    "\n",
    "        # Checking Termination Criteria, time is in Myr, amplitude is in meters\n",
    "        if time >= max_time || itime >= max_step || (X_contour[itime] - X_contour[1]) > options[\"thickening criteria\"]\n",
    "            terminate = true\n",
    "            ### Final Plots ###\n",
    "            get_plots(grid,Snew,Tnew,Xnew,\"final\")\n",
    "        end\n",
    "\n",
    "        if time == 0.0 || mod(itime,10) == 0 || terminate\n",
    "            last_plot = time \n",
    "            # Markers output\n",
    "            name1 = @sprintf(\"%s/markers.%04d.vtp\",output_dir,iout)\n",
    "            visualization(markers,time/seconds_in_year;filename=name1);\n",
    "            iout += 1\n",
    "        end\n",
    "\n",
    "        time += dt\n",
    "        if mod(itime,200) == 0\n",
    "            println(\"Ice shell as thicken by \",(X_contour[itime] - X_contour[1])/1e3,\"km\")\n",
    "            println(\"time = \",time/seconds_in_year,\" yr, \",time/seconds_in_year/1e3,\" Kyr, \",time/seconds_in_year/1e6,\" Myr\") \n",
    "        end\n",
    "        itime += 1\n",
    "    end\n",
    "    return grid,time_plot,X_contour_array,X_contour,itime,Tnew\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f5e379df-8c91-46ee-8d88-ca7fb64170fd",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "options = Dict()\n",
    "options[\"latent heat of fusion\"] = 3.34e5 #J/kg\n",
    "options[\"specific heat of ice\"] = 2.1e3 # J/kg*K (ice)\n",
    "options[\"density of ice\"] = 1e3 # kg/m^3\n",
    "options[\"thermal conductivity of ice\"] = 2.2 # W/m*K\n",
    "options[\"thermal diffusivity\"] = options[\"thermal conductivity of ice\"] / (options[\"density of ice\"]*options[\"specific heat of ice\"]) # m^2/s\n",
    "options[\"Tm\"] = 273.0 # K\n",
    "options[\"To\"] = 100.0 # K\n",
    "options[\"ym\"] = 1e4 # m \n",
    "# Define thickening criteria (this will stop the model when the ice has thicken by the value set by thickening criteria)\n",
    "options[\"thickening criteria\"] = 2e3 # m\n",
    "grid,time_plot,X_contour_array,X_contour,itime,Tnew = model_run(options);\n",
    "compute_stefan_temp_solution(grid,Tnew,X_contour,time_plot,itime)\n",
    "thickness_over_time(grid,X_contour_array,time_plot,itime)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.4",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
